buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
}

plugins {
    id 'java'
    id 'maven-publish'
}

version '0.7.0-SNAPSHOT'
group 'uk.ac.ebi.subs'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
    mavenLocal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-amqp:1.5.21.RELEASE")
    implementation("com.fasterxml.jackson.module:jackson-module-afterburner:2.7.1")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.10.0")
    testImplementation("org.springframework.boot:spring-boot-starter-test:1.5.21.RELEASE")
    testImplementation("com.google.code.gson:gson:2.7") //bug in spring boot, see https://github.com/spring-projects/spring-boot/issues/6502
}

task jarWithAddedManifest(type: Jar) {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Source-Compatibility': project.sourceCompatibility)
    }
    with jar
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'subs-messaging'

            pom {
                name = 'subs-messaging'
                description = 'Data Submission Portal - common library for message configuration'
                url = 'https://github.com/EMBL-EBI-SUBS/subs-messaging'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'subs-internal'
                        name = 'EMBL-EBI-SUBS'
                        email = 'subs-internal@ebi.ac.uk'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/EMBL-EBI-SUBS/subs-messaging.git'
                    developerConnection = 'scm:git:ssh://github.com/EMBL-EBI-SUBS/subs-messaging.git'
                    url = 'https://github.com/EMBL-EBI-SUBS/subs-messaging.git'
                }
            }

            from components.java
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

